#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

# LCD4Linux autoconf script
#
# Copyright (C) 1999, 2000, 2001, 2002, 2003 Michael Reinelt <reinelt@eunet.at>
# Copyright (C) 2004 The LCD4Linux Team <lcd4linux-devel@users.sourceforge.net>
#
# This file is part of LCD4Linux.
#
# LCD4Linux is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# LCD4Linux is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

AC_PREREQ(2.59)
AC_INIT([LCD4Linux],[0.10.0-RC2],[lcd4linux-users@lists.sourceforge.net])
AC_CONFIG_SRCDIR([lcd4linux.c])
AM_INIT_AUTOMAKE([lcd4linux],0.10.0-RC2)
AM_CONFIG_HEADER(config.h)

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

# dmalloc
AM_WITH_DMALLOC

# Checks for libraries.
AC_CHECK_LIB(m, log)

# curses
sinclude(curses.m4)
AC_CHECK_CURSES

# Checks for X11
AC_PATH_XTRA

# check for gd.h
AC_CHECK_HEADERS(gd/gd.h gd.h, [has_gd="true";break], [has_gd="false"])

# check for usb.h
AC_CHECK_HEADERS(usb.h, [has_usb="true"], [has_usb="false"])

# check for serdisplib
AC_CHECK_HEADERS(serdisplib/serdisp.h, [has_serdisplib="true"], [has_serdisplib="false"])

# check for python
AC_MSG_CHECKING(if python support is wanted)
AC_ARG_WITH(python,
[  --with-python           enable python support [[default=no]]],
[AC_MSG_RESULT($withval)], [AC_MSG_RESULT(no)])
if test "$with_python" = "yes"; then
  sinclude(python.m4)
  MY_PYTHON_DEVEL
fi

# drivers
sinclude(drivers.m4)

# plugins
sinclude(plugins.m4)

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h netdb.h netinet/in.h stdlib.h string.h sys/ioctl.h sys/socket.h sys/statfs.h sys/vfs.h sys/time.h syslog.h termios.h unistd.h])
AC_CHECK_HEADERS(sys/io.h asm/io.h)
AC_CHECK_HEADERS(linux/parport.h linux/ppdev.h)
AC_CHECK_HEADERS(asm/msr.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_TYPE_UID_T

# Checks for library functions.
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL

# uClibc has no getloadavg()
# AC_FUNC_GETLOADAVG sounds promising, but does not really work
#AC_FUNC_GETLOADAVG

#removed for uClibc compatibility
#AC_FUNC_MALLOC
#AC_FUNC_REALLOC

AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRFTIME
AC_FUNC_STRTOD
AC_CHECK_FUNCS([dup2 floor gethostbyname gettimeofday memset pow putenv regcomp socket sqrt strcasecmp strchr strdup strerror strncasecmp strndup strpbrk strrchr strstr strtol uname])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
